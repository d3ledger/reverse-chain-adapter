/*
 * Copyright D3 Ledger, Inc. All Rights Reserved.
 * SPDX-License-Identifier: Apache-2.0
 */

buildscript {
    ext.kotlin_version = '1.3.10'

    repositories {
        mavenCentral()
        jcenter()
        // gradle plugins repository
        gradlePluginPortal()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.1'
    }
}

plugins {
    id 'jp.co.soramitsu.sora-plugin' version '0.1.2'
}

apply plugin: "kotlin-spring" // See https://kotlinlang.org/docs/reference/compiler-plugins.html#kotlin-spring-compiler-plugin
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'application'

shadowDistZip.classifier = 'shadow'
shadowDistTar.classifier = 'shadow'
// name of jar file that the shadowJar plugin generates
shadowJar.archiveName = 'app.jar'

mainClassName = 'com.d3.reverse.ReverseChainAdapterMain'
group 'com.d3.reverse'

// sora-plugin configs
soramitsu {
    projectGroup = 'd3-deploy'
    docker {
        // docker tag
        tag = System.getenv("TAG")
        // jar file that is used in the generated Dockerfile
        jar = new File("build/libs/app.jar")
        // the rest in configured using env variables
    }
}

dependencies {
    // https://mvnrepository.com/artifact/org.springframework/spring-context
    compile group: 'org.springframework', name: 'spring-context', version: '5.1.4.RELEASE'
    compile project(':reverse-chain-adapter-client')
    implementation("com.github.d3ledger.notary:notary-commons:$notary_version")
    testCompile("com.github.d3ledger.notary:notary-iroha-integration-test:$notary_version")
    testCompile 'com.github.hyperledger.iroha-java:testcontainers:6.1.0'
}

sourceSets {
    integrationTest {
        kotlin {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDirs += 'src/integration-test/kotlin'
        }
        resources {
            srcDirs = ["src/integration-test/resources"]
        }
    }
}

task integrationTest(type: Test) {
    group = LifecycleBasePlugin.VERIFICATION_GROUP
    description = 'Runs reverse chain-adapter integration tests'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    // Enable JUnit5 tests
    useJUnitPlatform {
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task runReverseChainAdapter(type: JavaExec) {
    main = mainClassName
    classpath = sourceSets.main.runtimeClasspath
}

project.afterEvaluate {
    dockerfileCreate.dependsOn(shadowJar)
    integrationTest.dependsOn(dockerfileCreate)
    test.finalizedBy(integrationTest)
}